# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../access_token.gni")

config("pricacy") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
}

if (is_standard_system) {
  ohos_shared_library("libprivacy_sdk") {
    subsystem_name = "security"
    part_name = "access_token"
    innerapi_tags = [ "platformsdk" ]
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"
    if (!use_clang_coverage) {
      version_script = "libprivacy_sdk.map"
    }

    output_name = "libprivacy_sdk"

    public_configs = [ ":pricacy" ]

    include_dirs = [
      "${access_token_path}/frameworks/privacy/include",
      "${access_token_path}/frameworks/common/include",
      "${access_token_path}/interfaces/innerkits/accesstoken/include",
      "src",
    ]

    sources = [
      "src/on_permission_used_record_callback_stub.cpp",
      "src/perm_active_status_change_callback.cpp",
      "src/perm_active_status_change_callback_stub.cpp",
      "src/perm_active_status_customized_cbk.cpp",
      "src/privacy_death_recipient.cpp",
      "src/privacy_kit.cpp",
      "src/privacy_manager_client.cpp",
      "src/privacy_manager_proxy.cpp",
      "src/state_change_callback.cpp",
      "src/state_change_callback_stub.cpp",
      "src/state_customized_cbk.cpp",
    ]

    deps = [
      "${access_token_path}/frameworks/common:accesstoken_common_cxx",
      "${access_token_path}/frameworks/privacy:privacy_communication_adapter_cxx",
      "${access_token_path}/interfaces/innerkits/accesstoken:libaccesstoken_sdk",
    ]

    configs = [ "${access_token_path}/config:coverage_flags" ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "ipc:ipc_single",
      "samgr:samgr_proxy",
    ]

    cflags_cc = [
      "-DHILOG_ENABLE",
      "-DDEBUG_API_PERFORMANCE",
    ]

    if (security_component_enhance_enable == true) {
      cflags_cc += [ "-DSECURITY_COMPONENT_ENHANCE_ENABLE" ]
    }
  }
}
