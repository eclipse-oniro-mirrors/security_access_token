# Copyright (c) 2022-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/access_token/access_token.gni")
import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("privacy_manager.gni")

idl_gen_interface("privacy_manager_interface") {
  sources = [ "./idl/IPrivacyManager.idl" ]
  log_domainid = "0xD005A02"
  log_tag = "PRIVACY"
  subsystem_name = "security"
  part_name = "access_token"
}

config("privacy_manager_gen_config") {
  include_dirs = [ "${target_gen_dir}" ]
}

ohos_source_set("privacy_manager_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  output_values = get_target_outputs(":privacy_manager_interface")

  include_dirs = [
    "${access_token_path}/frameworks/privacy/include",
    "${access_token_path}/frameworks/common/include",
    "${access_token_path}/interfaces/innerkits/accesstoken/include",
    "${access_token_path}/interfaces/innerkits/privacy/include",
    "${access_token_path}/interfaces/innerkits/privacy/src",
    "include",
    "src",
  ]

  sources = filter_include(output_values, [ "*_proxy.cpp" ])

  deps = [
    ":privacy_manager_interface",
    "${access_token_path}/frameworks/privacy:privacy_communication_adapter_cxx",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  cflags_cc = access_token_cflags_cc

  subsystem_name = "security"
  part_name = "access_token"
}

ohos_source_set("privacy_manager_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  output_values = get_target_outputs(":privacy_manager_interface")

  include_dirs = [
    "${access_token_path}/frameworks/privacy/include",
    "${access_token_path}/frameworks/common/include",
    "${access_token_path}/interfaces/innerkits/accesstoken/include",
    "${access_token_path}/interfaces/innerkits/privacy/include",
    "${access_token_path}/interfaces/innerkits/privacy/src",
    "include",
    "src",
  ]

  sources = filter_include(output_values, [ "*_stub.cpp" ])

  deps = [
    ":privacy_manager_interface",
    "${access_token_path}/frameworks/privacy:privacy_communication_adapter_cxx",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  cflags_cc = access_token_cflags_cc
  if (build_variant == "user") {
    cflags_cc += [ "-DATM_BUILD_VARIANT_USER_ENABLE" ]
  }

  subsystem_name = "security"
  part_name = "access_token"
}

ohos_prebuilt_etc("privacy.rc") {
  source = "privacy.cfg"
  relative_install_dir = "init"
  subsystem_name = "security"
  part_name = "access_token"
}

group("privacymanager") {
  if (is_standard_system && ability_base_enable == true) {
    deps = [
      "${access_token_path}/services/privacymanager:privacy_manager_service",
      "${access_token_path}/services/privacymanager/sa_profile:privacy_sa_profile_standard",
    ]
  }
}

if (is_standard_system && ability_base_enable == true) {
  ohos_shared_library("privacy_manager_service") {
    subsystem_name = "security"
    part_name = "access_token"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"

    if (!use_clang_coverage) {
      version_script = "libprivacy_map"
      shlib_type = "sa"
    }

    # All configurations from privacy_manager_base_config
    sources = privacy_manager_base_config.sources
    include_dirs = privacy_manager_base_config.include_dirs
    deps = privacy_manager_base_config.deps
    external_deps = privacy_manager_base_config.external_deps

    cflags_cc = privacy_manager_base_config.cflags_cc
    configs = privacy_manager_base_config.configs
    defines = [ "FEATURE_DTMF_TONE" ]
  }
}
