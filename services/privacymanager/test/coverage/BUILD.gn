# Copyright (c) 2024-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../access_token.gni")

if (is_standard_system && ability_base_enable == true) {
  ohos_unittest("libprivacy_manager_service_coverage_test") {
    subsystem_name = "security"
    part_name = "access_token"
    module_out_path = part_name + "/" + part_name
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"

    include_dirs = [
      "${access_token_path}/frameworks/common/include",
      "${access_token_path}/interfaces/innerkits/nativetoken/include",
      "${access_token_path}/interfaces/innerkits/privacy/test/unittest/src",
      "${access_token_path}/frameworks/privacy/include",
      "${access_token_path}/interfaces/innerkits/accesstoken/include",
      "${access_token_path}/interfaces/innerkits/privacy/include",
      "${access_token_path}/interfaces/innerkits/privacy/src",
      "${access_token_path}/services/common/app_manager/include",
      "${access_token_path}/services/common/json_parse/include",
      "${access_token_path}/services/common/database/include",
      "${access_token_path}/services/common/handler/include",
      "${access_token_path}/services/common/libraryloader/include",
      "${access_token_path}/services/common/screenlock_manager/include",
      "${access_token_path}/services/common/utils/include",
      "${access_token_path}/services/privacymanager/include/active",
      "${access_token_path}/services/privacymanager/include/common",
      "${access_token_path}/services/privacymanager/include/database",
      "${access_token_path}/services/privacymanager/include/record",
      "${access_token_path}/services/privacymanager/include/service",
      "${access_token_path}/services/privacymanager/include/proxy",
      "${access_token_path}/services/privacymanager/include/sensitive/audio_manager",
      "${access_token_path}/services/privacymanager/include/sensitive/camera_manager",
      "${access_token_path}/services/accesstokenmanager/main/cpp/include/token",
    ]

    sources = [
      "${access_token_path}/interfaces/innerkits/privacy/test/unittest/src/privacy_test_common.cpp",
      "../../src/active/active_status_callback_manager.cpp",
      "../../src/active/perm_active_status_callback_death_recipient.cpp",
      "../../src/active/perm_active_status_change_callback_proxy.cpp",
      "../../src/active/state_change_callback_proxy.cpp",
      "../../src/common/constant.cpp",
      "../../src/database/data_translator.cpp",
      "../../src/database/permission_used_record_db.cpp",
      "../../src/database/privacy_field_const.cpp",
      "../../src/proxy/privacy_manager_proxy_death_param.cpp",
      "../../src/record/on_permission_used_record_callback_proxy.cpp",
      "../../src/record/permission_record.cpp",
      "../../src/record/permission_record_manager.cpp",
      "../../src/record/permission_record_set.cpp",
      "../../src/sensitive/audio_manager/audio_manager_adapter.cpp",
      "../../src/sensitive/camera_manager/camera_manager_adapter.cpp",
      "../../src/service/privacy_manager_service.cpp",
      "permission_record_manager_coverage_test.cpp",
      "sensitive_manager_coverage_test.cpp",
    ]

    cflags_cc = [ "-DHILOG_ENABLE" ]

    configs = [
      "${access_token_path}/config:coverage_flags",
      "${access_token_path}/services/privacymanager:privacy_manager_gen_config",
    ]

    deps = [
      "${access_token_path}/frameworks/common:accesstoken_common_cxx",
      "${access_token_path}/frameworks/privacy:privacy_communication_adapter_cxx",
      "${access_token_path}/interfaces/innerkits/accesstoken:libaccesstoken_sdk",
      "${access_token_path}/interfaces/innerkits/accesstoken:libtokenid_sdk",
      "${access_token_path}/interfaces/innerkits/nativetoken:libnativetoken_shared",
      "${access_token_path}/interfaces/innerkits/privacy:libprivacy_sdk",
      "${access_token_path}/interfaces/innerkits/token_setproc:libtoken_setproc",
      "${access_token_path}/services/common:accesstoken_service_common",
      "${access_token_path}/services/common/proxy_death:proxy_death_handler",
      "${access_token_path}/services/privacymanager:privacy_manager_service",
      "${access_token_path}/services/privacymanager:privacy_manager_stub",
    ]

    external_deps = [
      "ability_base:want",
      "access_token:libaccesstoken_sdk",
      "c_utils:utils",
      "googletest:gtest_main",
      "hilog:libhilog",
      "hisysevent:libhisysevent",
      "init:libbegetutil",
      "ipc:ipc_core",
      "safwk:system_ability_fwk",
      "samgr:samgr_proxy",
      "sqlite:sqlite",
    ]

    if (audio_framework_enable) {
      cflags_cc += [ "-DAUDIO_FRAMEWORK_ENABLE" ]
      external_deps += [ "audio_framework:audio_client" ]
    }

    if (camera_framework_enable) {
      cflags_cc += [ "-DCAMERA_FRAMEWORK_ENABLE" ]
      external_deps += [ "camera_framework:camera_framework" ]
    }

    if (eventhandler_enable == true) {
      cflags_cc += [ "-DEVENTHANDLER_ENABLE" ]
      external_deps += [ "eventhandler:libeventhandler" ]
    }
    if (common_event_service_enable) {
      cflags_cc += [ "-DCOMMON_EVENT_SERVICE_ENABLE" ]
      external_deps += [ "common_event_service:cesfwk_innerkits" ]
      sources += [ "../../src/common/privacy_common_event_subscriber.cpp" ]
    }
    if (theme_screenlock_mgr_enable) {
      cflags_cc += [ "-DTHEME_SCREENLOCK_MGR_ENABLE" ]
      external_deps += [ "screenlock_mgr:screenlock_client" ]
    }

    if (window_manager_enable && access_token_camera_float_window_enable) {
      cflags_cc += [ "-DCAMERA_FLOAT_WINDOW_ENABLE" ]
      include_dirs +=
          [ "${access_token_path}/services/common/window_manager/include" ]
      sources += [
        "${access_token_path}/services/common/window_manager/src/privacy_mock_session_manager_proxy.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_scene_session_manager_lite_proxy.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_scene_session_manager_proxy.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_session_manager_proxy.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_window_manager_agent.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_window_manager_client.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_window_manager_death_recipient.cpp",
        "${access_token_path}/services/common/window_manager/src/privacy_window_manager_proxy.cpp",
      ]
      external_deps += [ "window_manager:libwsutils" ]
    }
    if (access_token_app_security_privacy_service_enable) {
      cflags_cc += [ "-DAPP_SECURITY_PRIVACY_SERVICE" ]
    } else {
      include_dirs +=
          [ "${access_token_path}/services/common/ability_manager/include" ]
    }
  }
}
