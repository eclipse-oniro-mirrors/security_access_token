# Copyright (c) 2021-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/access_token/access_token.gni")
import("//build/ohos.gni")
import("tokensync_manager.gni")

ohos_prebuilt_etc("token_sync.rc") {
  source = "token_sync.cfg"
  relative_install_dir = "init"
  subsystem_name = "security"
  part_name = "access_token"
}

group("tokensyncmanager") {
  if (is_standard_system && token_sync_enable == true) {
    deps = [
      ":token_sync_manager_service",
      "sa_profile:tokensync_sa_profile_standard",
    ]
  }
}

if (token_sync_enable == true) {
  ohos_shared_library("token_sync_manager_service") {
    subsystem_name = "security"
    part_name = "access_token"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"

    if (!use_clang_coverage) {
      version_script = "tokensync_manager_service.map"
      shlib_type = "sa"
    }

    # All configurations from tokensync_manager_base_config
    # including sources, includes, deps, external_deps with all conditional logic
    sources = tokensync_manager_base_config.sources
    include_dirs = tokensync_manager_base_config.include_dirs
    deps = tokensync_manager_base_config.deps
    external_deps = tokensync_manager_base_config.external_deps

    cflags_cc = tokensync_manager_base_config.cflags_cc
    cflags_cc += [
      "-DDEBUG_API_PERFORMANCE",
      "-fvisibility=hidden",
    ]
    if (build_variant == "user") {
      cflags_cc += [ "-DATM_BUILD_VARIANT_USER_ENABLE" ]
    }
    configs = tokensync_manager_base_config.configs
  }
}
