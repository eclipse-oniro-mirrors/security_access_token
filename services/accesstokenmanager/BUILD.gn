# Copyright (c) 2021-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/access_token/access_token.gni")
import("//build/ohos.gni")
import("accesstoken_manager.gni")

ohos_prebuilt_etc("access_token.rc") {
  source = "access_token.cfg"
  relative_install_dir = "init"
  subsystem_name = "security"
  part_name = "access_token"
}

ohos_prebuilt_etc("permission_definition_config") {
  source = "permission_definitions.json"
  part_name = "access_token"
  subsystem_name = "security"
  relative_install_dir = "access_token"
}

if (is_standard_system) {
  ohos_shared_library("accesstoken_manager_service") {
    subsystem_name = "security"
    part_name = "access_token"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"
    if (!use_clang_coverage) {
      version_script = "accesstoken_manager_service.map"
      shlib_type = "sa"
    }

    # All configurations from accesstoken_manager_base_config
    # including sources, includes, deps, external_deps with all conditional logic
    sources = accesstoken_manager_base_config.sources
    include_dirs = accesstoken_manager_base_config.include_dirs
    deps = accesstoken_manager_base_config.deps
    external_deps = accesstoken_manager_base_config.external_deps

    cflags_cc = accesstoken_manager_base_config.cflags_cc
    cflags_cc += [ "-fvisibility=hidden" ]
    if (build_variant == "user") {
      cflags_cc += [ "-DATM_BUILD_VARIANT_USER_ENABLE" ]
    }
    configs = accesstoken_manager_base_config.configs
  }
}

if (is_standard_system && lite_device_enable) {
  config("accesstoken_rdb_config") {
    visibility = [ ":*" ]
    include_dirs = [ "include" ]
  }

  ohos_shared_library("accesstoken_db_loader") {
    subsystem_name = "security"
    part_name = "access_token"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    branch_protector_ret = "pac_ret"

    configs = [
      "${access_token_path}/config:access_token_compile_flags",
      "${access_token_path}/config:coverage_flags",
    ]
    public_configs = [ ":accesstoken_rdb_config" ]

    include_dirs = [
      "${access_token_path}/frameworks/common/include",
      "${access_token_path}/interfaces/innerkits/accesstoken/include",
      "${access_token_path}/services/common/database/include",
      "main/cpp/include/database",
      "main/cpp/include/dfx",
    ]

    sources = [
      "${access_token_path}/services/common/database/src/generic_values.cpp",
      "${access_token_path}/services/common/database/src/variant_value.cpp",
      "main/cpp/src/database/access_token_db.cpp",
      "main/cpp/src/database/access_token_db_loader.cpp",
      "main/cpp/src/database/access_token_db_util.cpp",
      "main/cpp/src/database/access_token_open_callback.cpp",
      "main/cpp/src/database/token_field_const.cpp",
    ]

    cflags_cc = access_token_cflags_cc

    deps = [
      "${access_token_path}/frameworks/common:accesstoken_common_cxx",
      "${access_token_path}/frameworks/common:accesstoken_hisysevent",
    ]

    external_deps = accesstoken_manager_base_config.external_deps
  }
}
