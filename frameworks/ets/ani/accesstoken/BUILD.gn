# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/access_token/access_token.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")

ohos_shared_library("accesstoken_ani") {
  shlib_type = "ani"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  cflags_cc = [ "-DHILOG_ENABLE" ]
  include_dirs = [
    "${access_token_path}/frameworks/common/include",
    "${access_token_path}/interfaces/innerkits/accesstoken/include",
    "./include",
    "${access_token_path}/interfaces/innerkits/token_setproc/include",
    "${access_token_path}/interfaces/innerkits/token_callback/include",
    "${access_token_path}/frameworks/ets/ani/common/include",
  ]
  sources = [
    "src/ani_ability_access_ctrl.cpp",
    "src/ani_common.cpp",
    "src/ani_request_global_switch_on_setting.cpp",
    "src/ani_request_permission.cpp",
    "src/ani_request_permission_on_setting.cpp",
  ]

  deps = [
    "${access_token_path}/frameworks/ets/ani/common:libani_common",
    "${access_token_path}/interfaces/innerkits/accesstoken:libaccesstoken_sdk",
    "${access_token_path}/interfaces/innerkits/token_callback:libtoken_callback_sdk",
    "${access_token_path}/interfaces/innerkits/token_setproc:libtokensetproc_shared",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:ani_base_context",
    "ability_runtime:appkit_native",
    "ability_runtime:ui_extension",
    "ace_engine:ace_uicontent",
    "c_utils:utils",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "init:libbegetutil",
    "ipc:ipc_single",
    "napi:ace_napi",
    "runtime_core:ani",
    "runtime_core:libarkruntime",
  ]

  if (eventhandler_enable == true) {
    cflags_cc += [ "-DEVENTHANDLER_ENABLE" ]
    external_deps += [ "eventhandler:libeventhandler" ]
  }

  part_name = "access_token"
  subsystem_name = "security"
}

generate_static_abc("ability_access_ctrl") {
  base_url = "./ets"
  files = [ "./ets/@ohos.abilityAccessCtrl.ets" ]

  is_boot_abc = "True"
  device_dst_file = "/system/framework/ability_access_ctrl.abc"
}

ohos_prebuilt_etc("ability_access_ctrl_etc") {
  source = "$target_out_dir/ability_access_ctrl.abc"
  deps = [ ":ability_access_ctrl" ]
  module_install_dir = "framework"
  part_name = "access_token"
  subsystem_name = "security"
}

group("accesstoken_ets") {
  deps = [
    ":ability_access_ctrl_etc",
    ":accesstoken_ani",
  ]
}
